ПодключитьСценарий("..\src\ПарсерВстроенногоЯзыка\Ext\ObjectModule.bsl", "Парсер");
ПодключитьСценарий("..\plugins\ДетекторНеиспользуемыхПеременных\src\ДетекторНеиспользуемыхПеременных\Ext\ObjectModule.bsl", "ДетекторНеиспользуемыхПеременных");
ПодключитьСценарий("..\plugins\ДетекторОшибочныхЗамыкающихКомментариев\src\ДетекторОшибочныхЗамыкающихКомментариев\Ext\ObjectModule.bsl", "ДетекторОшибочныхЗамыкающихКомментариев");
ПодключитьСценарий("..\plugins\ДетекторФункцийБезВозвратаВКонце\src\ДетекторФункцийБезВозвратаВКонце\Ext\ObjectModule.bsl", "ДетекторФункцийБезВозвратаВКонце");

Если АргументыКоманднойСтроки.Количество() <> 2 Тогда
	ВызватьИсключение "Укажите в качестве параметров список модулей bsl через ';' и имя файла отчета";
КонецЕсли;

Пути = СтрРазделить(АргументыКоманднойСтроки[0], ";");
Файлы = Новый Массив;
Для Каждого Путь Из Пути Цикл
	Файлы.Добавить(Новый Файл(Путь));
КонецЦикла;

Парсер = Новый Парсер;
Парсер.СтрогийРежим = Ложь;

Плагины = Новый Массив;
Плагины.Добавить(Новый ДетекторНеиспользуемыхПеременных);
Плагины.Добавить(Новый ДетекторОшибочныхЗамыкающихКомментариев);
Плагины.Добавить(Новый ДетекторФункцийБезВозвратаВКонце);

ЧтениеТекста = Новый ЧтениеТекста;

Отчет = Новый Массив;

Для Каждого Файл Из Файлы Цикл
	Если Файл.ЭтоФайл() Тогда
		ЧтениеТекста.Открыть(Файл.ПолноеИмя, "UTF-8");
		Исходник = ЧтениеТекста.Прочитать();
		Попытка
			Парсер.Пуск(Исходник, Плагины);
			Для Каждого Плагин Из Плагины Цикл
				Результат = Плагин.Закрыть();
				Если ЗначениеЗаполнено(Результат) Тогда
					Отчет.Добавить(Символы.ПС);
					Отчет.Добавить(Файл.ПолноеИмя);
					Отчет.Добавить(Символы.ПС);
					Отчет.Добавить(Результат);
					Отчет.Добавить(Символы.ПС);
				КонецЕсли;
			КонецЦикла;
		Исключение
			Отчет.Добавить(Символы.ПС);
			Отчет.Добавить(Файл.ПолноеИмя);
			Отчет.Добавить(Символы.ПС);
			Отчет.Добавить("ОШИБКА:");
			Отчет.Добавить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отчет.Добавить(Символы.ПС);
		КонецПопытки;
		ЧтениеТекста.Закрыть();
	КонецЕсли;
КонецЦикла;

ЗаписьТекста = Новый ЗаписьТекста(АргументыКоманднойСтроки[1], "UTF-8",, Истина);
ЗаписьТекста.Записать(СтрСоединить(Отчет));
ЗаписьТекста.Закрыть();

Сообщить("Проверка закончена. Отчет о проверке находится в файле 'Отчет.txt'");
