
// Пример диагностики, которая не только находит ошибку, но и предлагает замену (автоматическое исправление).
// Плагин обнаруживает использование конструкторов структур, регистрирует ошибку и замену в виде вставки ключей.
// Пример:
//   Сотрудник = Новый Структура("Имя, Фамилия", "Иван");
// будет заменено на:
//   Сотрудник = Новый Структура;
//   Сотрудник.Вставить("Имя", "Иван");
//   Сотрудник.Вставить("Фамилия", Неопределено);

Перем Токены;
Перем Типы;
Перем ТаблицаТокенов;
Перем Исходник;
Перем ТаблицаОшибок;
Перем ТаблицаЗамен;

Процедура Открыть(Парсер, Параметры) Экспорт
	
	Токены = Парсер.Токены();
	ТаблицаТокенов = Парсер.ТаблицаТокенов();
	Исходник = Парсер.Исходник();
	Типы = Парсер.Типы();
	ТаблицаОшибок = Парсер.ТаблицаОшибок();
	ТаблицаЗамен = Парсер.ТаблицаЗамен();
			
КонецПроцедуры // Открыть()

Функция Закрыть() Экспорт
	
	Возврат Неопределено;	
	
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьОператорПрисваивания");
	Возврат Подписки;
КонецФункции // Подписки()

#Область РеализацияПодписок

Процедура ПосетитьОператорПрисваивания(ОператорПрисваивания) Экспорт
	
	Выражение = ОператорПрисваивания.ПравыйОперанд;
		
	Если Выражение.Тип = Типы.ВыражениеНовый
		И Выражение.Имя = "Структура"
		И Выражение.Аргументы <> Неопределено
		И Выражение.Аргументы.Количество() > 0 Тогда
		
		ПервыйАргумент = Выражение.Аргументы[0];
		
		Если ПервыйАргумент.Тип = Типы.ВыражениеСтроковое Тогда
			
			Идентификатор = ОператорПрисваивания.ЛевыйОперанд;
			
			Если Идентификатор.Аргументы = Неопределено
				И Идентификатор.Хвост.Количество() = 0 Тогда
				
				ПервыйТокенИдентификатора = Идентификатор.Начало;
				ДлинаОтступа = ПервыйТокенИдентификатора.НомерКолонки - 1;
				Отступ = Сред(Исходник, ПервыйТокенИдентификатора.Позиция - ДлинаОтступа, ДлинаОтступа);
				
				Если ПустаяСтрока(Отступ) Тогда
					
					СписокКлючей = Новый Массив;
					
					Для Каждого Элемент Из ПервыйАргумент.Элементы Цикл
						СписокКлючей.Добавить(Элемент.Значение);
					КонецЦикла; 
					
					Ключи = СтрРазделить(СтрСоединить(СписокКлючей), ",", Ложь);
					
					Буфер = Новый Массив;
					
					КоличествоЗначений = Выражение.Аргументы.Количество() - 1;
					
					Буфер.Добавить("Новый Структура;");
					
					Для Индекс = 0 По Ключи.Количество() - 1 Цикл
						
						Ключ = СокрЛП(Ключи[Индекс]);
						
						Буфер.Добавить(Символы.ПС);
						Буфер.Добавить(Отступ);
						ПервыйТокен = Идентификатор.Начало;
						ПоследнийТокен = Идентификатор.Конец;
						Буфер.Добавить(Сред(
							Исходник,
							ПервыйТокен.Позиция,
							ПоследнийТокен.Позиция + ПоследнийТокен.Длина - ПервыйТокен.Позиция
						));
						Буфер.Добавить(СтрШаблон(".Вставить(""%1"", ", Ключ));
						
						Если Индекс < КоличествоЗначений И Выражение.Аргументы[Индекс + 1] <> Неопределено Тогда
							Аргумент = Выражение.Аргументы[Индекс + 1]; 
							ПервыйТокен = Аргумент.Начало;
							ПоследнийТокен = Аргумент.Конец;
							Буфер.Добавить(Сред(
								Исходник,
								ПервыйТокен.Позиция,
								ПоследнийТокен.Позиция + ПоследнийТокен.Длина - ПервыйТокен.Позиция
							));
						Иначе
							Буфер.Добавить("Неопределено");
						КонецЕсли; 
						
						Буфер.Добавить(");");
						
					КонецЦикла; 
					
					Текст = СтрСоединить(Буфер);
					
					ПервыйТокен = Выражение.Начало;
					ПоследнийТокен = Выражение.Конец;
					СледующийТокен = ТаблицаТокенов[Выражение.Конец.Индекс + 1];
					
					Если СледующийТокен.Токен = Токены.ТочкаСЗапятой Тогда
						ПоследнийТокен = СледующийТокен;
					КонецЕсли; 
					
					Ошибка("Обнаружен конструктор структур. Доступно автоисправление", Выражение.Начало, Выражение.Конец);
					Замена(Текст, ПервыйТокен, ПоследнийТокен);
					
				КонецЕсли; 
				
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура Ошибка(Текст, Начало, Конец = Неопределено, ЕстьЗамена = Ложь)
	Ошибка = ТаблицаОшибок.Добавить();
	Ошибка.Источник = "ДетекторКонструкторовСтруктур";
	Ошибка.Текст = Текст;
	Ошибка.ПозицияНачала = Начало.Позиция;
	Ошибка.НомерСтрокиНачала = Начало.НомерСтроки;
	Ошибка.НомерКолонкиНачала = Начало.НомерКолонки;
	Если Конец = Неопределено Или Конец = Начало Тогда
		Ошибка.ПозицияКонца = Начало.Позиция + Начало.Длина;
		Ошибка.НомерСтрокиКонца = Начало.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Начало.НомерКолонки + Начало.Длина;
	Иначе
		Ошибка.ПозицияКонца = Конец.Позиция + Конец.Длина;
		Ошибка.НомерСтрокиКонца = Конец.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Конец.НомерКолонки + Конец.Длина;
	КонецЕсли;
	Ошибка.ЕстьЗамена = ЕстьЗамена;
КонецПроцедуры

Процедура Замена(Текст, Начало, Конец = Неопределено)
	НоваяЗамена = ТаблицаЗамен.Добавить();
	НоваяЗамена.Источник = "ДетекторКонструкторовСтруктур";
	НоваяЗамена.Текст = Текст;
	НоваяЗамена.Позиция = Начало.Позиция;
	Если Конец = Неопределено Тогда
		НоваяЗамена.Длина = Начало.Длина;
	Иначе
		НоваяЗамена.Длина = Конец.Позиция + Конец.Длина - Начало.Позиция;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти // РеализацияПодписок

