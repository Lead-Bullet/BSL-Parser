
 // Простая и эффективная проверка на наличие возврата из функции.
 // Чаще всего возврат просто забывают написать.
 // Нет никакого смысла разбирать сложные случаи,
 // когда пропущен возврат в одной из логических ветвей,
 // так как такие ошибки встречаются редко.
 // Гораздо проще договориться с командой, что функция всегда
 // должна оканчиваться инструкцией Возврат и автоматически
 // проверять код подобным плагином.

Перем Узлы;
Перем Результат;

Процедура Инициализировать(Парсер) Экспорт
	Узлы = Парсер.Узлы();
	Результат = Новый Массив;
КонецПроцедуры

Функция Закрыть() Экспорт
	Возврат СтрСоединить(Результат, Символы.ПС);
КонецФункции

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Add("ПосетитьОбъявлениеМетода");
	Возврат Подписки;
КонецФункции

Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода, Стек, Счетчики) Экспорт
	Перем КоличествоОператоров;
	Если ОбъявлениеМетода.Сигнатура.Тип <> Узлы.СигнатураФункции Тогда 
		Возврат;	
	КонецЕсли;
	КоличествоОператоров = ОбъявлениеМетода.Операторы.Количество();
	Если КоличествоОператоров = 0 Или ОбъявлениеМетода.Операторы[КоличествоОператоров - 1].Тип <> Узлы.ОператорВозврат Тогда 
		Результат.Добавить(СтрШаблон("Последней инструкцией функции `%1()` должен быть `Возврат`""", ОбъявлениеМетода.Сигнатура.Имя));	
	КонецЕсли;
КонецПроцедуры