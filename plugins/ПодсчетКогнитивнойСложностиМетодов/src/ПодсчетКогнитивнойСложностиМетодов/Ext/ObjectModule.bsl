
 // Подсчет когнитивной сложности методов (выводятся только > 0).
 // Не учитываются косвенные рекурсивные вызовы.

Перем Узлы;
Перем Токены;
Перем Результат;

Перем Уровень;
Перем КогнитивнаяСложность;
Перем ТекущийМетод;
Перем УровеньВыражения;

Процедура Инициализировать(Парсер) Экспорт
	Узлы = Парсер.Узлы();
	Токены = Парсер.Токены();
	Результат = Новый Массив;
	Уровень = 1;
	КогнитивнаяСложность = 0;
	УровеньВыражения = 0;
КонецПроцедуры // Init()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Add("ПосетитьОбъявлениеМетода");
	Подписки.Add("ПокинутьОбъявлениеМетода");
	Подписки.Add("ПосетитьВыражениеБинарное");
	Подписки.Add("ПокинутьВыражениеБинарное");
	Подписки.Add("ПосетитьВыражениеТернарное");
	Подписки.Add("ПосетитьОператорЕсли");
	Подписки.Add("ПокинутьОператорЕсли");
	Подписки.Add("ПосетитьОператорИначеЕсли");
	Подписки.Add("ПосетитьОператорИначе");
	Подписки.Add("ПосетитьОператорПока");
	Подписки.Add("ПокинутьОператорПока");
	Подписки.Add("ПосетитьОператорДля");
	Подписки.Add("ПокинутьОператорДля");
	Подписки.Add("ПосетитьОператорДляКаждого");
	Подписки.Add("ПокинутьОператорДляКаждого");
	Подписки.Add("ПосетитьОператорИсключение");
	Подписки.Add("ПосетитьОператорВызоваПроцедуры");
	Подписки.Add("ПосетитьОператорПерейти");
	Подписки.Add("ПосетитьОператорПрервать");
	Подписки.Add("ПосетитьОператорПродолжить");
	Возврат Подписки;
КонецФункции // Подписки()

Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода, Стек, Счетчики) Экспорт
	ТекущийМетод = ОбъявлениеМетода.Сигнатура;
КонецПроцедуры // ПосетитьОбъявлениеМетода()

Процедура ПокинутьОбъявлениеМетода(ОбъявлениеМетода, Стек, Счетчики) Экспорт
	Если КогнитивнаяСложность > 0 Тогда 
		Результат.Add(StrTemplate("Когнитивная сложность метода %1() равна %2", ОбъявлениеМетода.Сигнатура.Имя, КогнитивнаяСложность));	
	КонецЕсли;
	Уровень = 1;
	КогнитивнаяСложность = 0;
КонецПроцедуры // ПокинутьОбъявлениеМетода()

Процедура ПосетитьВыражениеБинарное(ВыражениеБинарное, Стек, Счетчики) Экспорт
	Если УровеньВыражения = 0 Тогда  // только для корневого
		СписокОпераций = Новый Массив;
		ПостроитьСписокОпераций(СписокОпераций, ВыражениеБинарное);
		ТекущаяОперация = Неопределено;
		Для Каждого Операция Из СписокОпераций Цикл 
			Если Операция <> ТекущаяОперация Тогда 
				ТекущаяОперация = Операция;
				Если ТекущаяОперация = Токены.Или
					Или ТекущаяОперация = Токены.И Тогда 
					КогнитивнаяСложность = КогнитивнаяСложность + 1;				
				КонецЕсли;			
			КонецЕсли;		
		КонецЦикла;	
	КонецЕсли;
	УровеньВыражения = УровеньВыражения + 1;
КонецПроцедуры // ПосетитьВыражениеБинарное()

Процедура ПокинутьВыражениеБинарное(ВыражениеБинарное, Стек, Счетчики) Экспорт
	УровеньВыражения = УровеньВыражения - 1;
КонецПроцедуры // ПокинутьВыражениеБинарное()

Функция ПостроитьСписокОпераций(СписокОпераций, ВыражениеБинарное)
	Если ВыражениеБинарное.ЛевыйОперанд.Тип = Узлы.ВыражениеБинарное Тогда 
		ПостроитьСписокОпераций(СписокОпераций, ВыражениеБинарное.ЛевыйОперанд);	
	КонецЕсли;
	СписокОпераций.Add(ВыражениеБинарное.Операция);
	Если ВыражениеБинарное.ПравыйОперанд.Тип = Узлы.ВыражениеБинарное Тогда 
		ПостроитьСписокОпераций(СписокОпераций, ВыражениеБинарное.ПравыйОперанд);	
	КонецЕсли;
КонецФункции // ПостроитьСписокОпераций()

Процедура ПосетитьВыражениеТернарное(ВыражениеТернарное, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + Уровень;
КонецПроцедуры // ПосетитьВыражениеТернарное()

Процедура ПосетитьОператорВызоваПроцедуры(ОператорВызоваПроцедуры, Стек, Счетчики) Экспорт
	Если ОператорВызоваПроцедуры.Идентификатор.Голова.Объявление = ТекущийМетод Тогда 
		КогнитивнаяСложность = КогнитивнаяСложность + 1;	
	КонецЕсли;
КонецПроцедуры // ПосетитьОператорВызоваПроцедуры()

Процедура ПосетитьОператорЕсли(ОператорЕсли, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + Уровень;
	Уровень = Уровень + 1;
КонецПроцедуры // ПосетитьОператорЕсли()

Процедура ПокинутьОператорЕсли(ОператорЕсли, Стек, Счетчики) Экспорт
	Уровень = Уровень - 1;
КонецПроцедуры // ПокинутьОператорЕсли()

Процедура ПосетитьОператорИначеЕсли(ОператорИначеЕсли, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + 1;
КонецПроцедуры // ПосетитьОператорИначеЕсли()

Процедура ПосетитьОператорИначе(ОператорИначе, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + 1;
КонецПроцедуры // ПосетитьОператорИначе()

Процедура ПосетитьОператорПока(ОператорПока, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + Уровень;
	Уровень = Уровень + 1;
КонецПроцедуры // ПосетитьОператорПока()

Процедура ПокинутьОператорПока(ОператорПока, Стек, Счетчики) Экспорт
	Уровень = Уровень - 1;
КонецПроцедуры // ПокинутьОператорПока()

Процедура ПосетитьОператорДля(ОператорДля, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + Уровень;
	Уровень = Уровень + 1;
КонецПроцедуры // ПосетитьОператорДля()

Процедура ПокинутьОператорДля(ОператорДля, Стек, Счетчики) Экспорт
	Уровень = Уровень - 1;
КонецПроцедуры // ПокинутьОператорДля()

Процедура ПосетитьОператорДляКаждого(ОператорДляКаждого, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + Уровень;
	Уровень = Уровень + 1;
КонецПроцедуры // ПосетитьОператорДляКаждого()

Процедура ПокинутьОператорДляКаждого(ОператорДляКаждого, Стек, Счетчики) Экспорт
	Уровень = Уровень - 1;
КонецПроцедуры // ПокинутьОператорДляКаждого()

Процедура ПосетитьОператорИсключение(ОператорИсключение, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + Уровень;
	Уровень = Уровень + 1;
КонецПроцедуры // ПосетитьОператорИсключение()

Процедура ПосетитьОператорПерейти(ОператорПерейти, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + 1;
КонецПроцедуры // ПосетитьОператорПерейти()

Процедура ПосетитьОператорПрервать(ОператорПрервать, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + 1;
КонецПроцедуры // ПосетитьОператорПрервать()

Процедура ПосетитьОператорПродолжить(ОператорПродолжить, Стек, Счетчики) Экспорт
	КогнитивнаяСложность = КогнитивнаяСложность + 1;
КонецПроцедуры // ПосетитьОператорПродолжить()

Функция Закрыть() Экспорт
	Возврат СтрСоединить(Результат, Символы.ПС);
КонецФункции