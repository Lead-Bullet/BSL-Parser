
// Проверка комментариев в окончаниях инструкций

Перем Узлы;
Перем Токены;
Перем Исходник;
Перем ТаблицаТокенов;
Перем ТаблицаОшибок;

Перем УровеньОбласти;
Перем СтекОбластей;

Процедура Инициализировать(Парсер, Параметры) Экспорт

	Узлы = Парсер.Узлы();
	Токены = Парсер.Токены();
	Исходник = Парсер.Исходник();
	ТаблицаТокенов = Парсер.ТаблицаТокенов();
	ТаблицаОшибок = Парсер.ТаблицаОшибок();

	УровеньОбласти = 0;
	СтекОбластей = Новый Соответствие;

КонецПроцедуры

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьОбъявлениеМетода");
	Подписки.Добавить("ПосетитьИнструкциюПрепроцессора");
	Возврат Подписки;
КонецФункции

Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода) Экспорт

	СледующийТокен = ТаблицаТокенов[ОбъявлениеМетода.Конец + 1];

	Если СледующийТокен.Токен = Токены.Комментарий
		И СледующийТокен.НомерСтроки = ТаблицаТокенов[ОбъявлениеМетода.Конец].НомерСтроки Тогда

		Комментарий = СокрП(Сред(Исходник, СледующийТокен.Начало, СледующийТокен.Длина));

		Если Комментарий <> СтрШаблон(" %1%2", ОбъявлениеМетода.Сигнатура.Имя, "()") Тогда
			Сообщение = СтрШаблон(
				"Метод `%1()` имеет неправильный замыкающий комментарий в строке %2",
				ОбъявлениеМетода.Сигнатура.Имя,
				СледующийТокен.НомерСтроки
			);
			Ошибка(Сообщение, ОбъявлениеМетода.Конец + 1);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ПосетитьИнструкциюПрепроцессора(ИнструкцияПрепроцессора) Экспорт

	Если ИнструкцияПрепроцессора.Тип = Узлы.ИнструкцияПрепроцессораОбласть Тогда

		УровеньОбласти = УровеньОбласти + 1;
		СтекОбластей[УровеньОбласти] = ИнструкцияПрепроцессора.Имя;

	ИначеЕсли ИнструкцияПрепроцессора.Тип = Узлы.ИнструкцияПрепроцессораКонецОбласти Тогда
		СледующийТокен = ТаблицаТокенов[ИнструкцияПрепроцессора.Конец + 1];

		Если СледующийТокен.Токен = Токены.Комментарий
			И СледующийТокен.НомерСтроки = ТаблицаТокенов[ИнструкцияПрепроцессора.Конец].НомерСтроки Тогда

			Комментарий = СокрП(Сред(Исходник, СледующийТокен.Начало, СледующийТокен.Длина));
			ИмяОбласти = СтекОбластей[УровеньОбласти];

			Если Комментарий <> СтрШаблон(" %1", ИмяОбласти) Тогда
				Сообщение = СтрШаблон(
					"Область `%1` имеет неправильный замыкающий комментарий в строке %2:",
					ИмяОбласти,
					СледующийТокен.НомерСтроки
				);
				Ошибка(Сообщение, ИнструкцияПрепроцессора.Конец + 1);
			КонецЕсли;

		КонецЕсли;

		УровеньОбласти = УровеньОбласти - 1;

	КонецЕсли;

КонецПроцедуры

Процедура Ошибка(Текст, ИндексНачала, ИндексКонца = Неопределено)
	Начало = ТаблицаТокенов[ИндексНачала];
	Ошибка = ТаблицаОшибок.Добавить();
	Ошибка.Источник = "ДетекторОшибочныхЗамыкающихКомментариев";
	Ошибка.Текст = Текст;
	Ошибка.ПозицияНачала = Начало.Начало;
	Ошибка.НомерСтрокиНачала = Начало.НомерСтроки;
	Ошибка.НомерКолонкиНачала = Начало.НомерКолонки;
	Если ИндексКонца = Неопределено Или ИндексКонца = ИндексНачала Тогда
		Ошибка.ПозицияКонца = Начало.Конец;
		Ошибка.НомерСтрокиКонца = Начало.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Начало.НомерКолонки + Начало.Длина;
	Иначе
		Конец = ТаблицаТокенов[ИндексКонца];
		Ошибка.ПозицияКонца = Конец.Конец;
		Ошибка.НомерСтрокиКонца = Конец.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Конец.НомерКолонки + Конец.Длина;
	КонецЕсли;
КонецПроцедуры