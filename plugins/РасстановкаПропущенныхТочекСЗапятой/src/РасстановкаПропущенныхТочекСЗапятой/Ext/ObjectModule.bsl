
Перем Токены;
Перем Узлы;
Перем ТаблицаТокенов;
Перем Исходник;

Перем СписокТокенов;

Процедура Инициализировать(Парсер, Параметры) Экспорт
	Токены = Парсер.Токены();
	ТаблицаТокенов = Парсер.ТаблицаТокенов();
	Исходник = Парсер.Исходник();
	Узлы = Парсер.Узлы();
	СписокТокенов = Новый Массив;
КонецПроцедуры // Инициализировать()

Функция Закрыть() Экспорт
	
	Если СписокТокенов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Массив;
	
	ПозицияНачала = 1;
	
	Для Индекс = 0 По СписокТокенов.Количество() - 1 Цикл
		
		Токен = СписокТокенов[Индекс];
		
		Результат.Добавить(Сред(Исходник, ПозицияНачала, Токен.Конец - ПозицияНачала));
		Результат.Добавить(";");
		
		ПозицияНачала = Токен.Конец;
		
	КонецЦикла;
	
	Результат.Добавить(Сред(Исходник, ПозицияНачала));
	
	Возврат СтрСоединить(Результат);
	
КонецФункции // Закрыть()

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПокинутьОператоры");
	Возврат Подписки;
КонецФункции // Подписки()

#Область РеализацияПодписок

Процедура ПокинутьОператоры(Операторы) Экспорт

	Для Каждого Оператор Из Операторы Цикл

		ПоследнийТокен = ТаблицаТокенов[Оператор.Конец];
		СледующийТокен = ТаблицаТокенов[Оператор.Конец + 1];

		Если СледующийТокен.Токен <> Токены.ТочкаСЗапятой И Не СтрНачинаетсяС(Оператор.Тип, "ИнструкцияПрепроцессора") Тогда

			СписокТокенов.Добавить(ПоследнийТокен);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ПосетитьОператоры()

#КонецОбласти // РеализацияПодписок

