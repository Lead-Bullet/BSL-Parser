# Книга Джедая

## Содержание

1. [Парсер](#парсер)  
    1.1 [Общие сведения](#общие-сведения)  
    1.2 [Программный интерфейс парсера](#программный-интерфейс-парсера)  
    * [Основные методы](#основные-методы)  
    * [Дополнительные методы](#дополнительные-методы)  
    * [Продвинутые методы](#продвинутые-методы)  
    * [Служебные методы](#служебные-методы)  
    * [Параметры](#параметры)
2. [Плагины](#плагины)
3. [Бакенды](#бакенды)

## Парсер

### Общие сведения

Парсер полностью реализован в виде одной обработки `ПарсерВстроенногоЯзыка.epf`, которая предоставляет три механизма:

* Разбор исходного кода на встроенном языке платформы 1С:Предприятие 8.3 с результатом в виде AST.
* Встроенный механизм обхода AST для плагинов.
* Встроенный механизм замен участков текста в исходнике.

На платформе 1С:Предприятие 8.3 создать экземпляр парсера можно следующим образом:

```bsl
Путь = "C:\git\bsparser\build\ПарсерВстроенногоЯзыка.epf";
Парсер = ВнешниеОбработки.Создать(Путь, Ложь);
```

В скрипте на OneScript создать экземпляр парсера можно следующим образом:

```bsl
ПодключитьСценарий("C:\git\bsparser\src\ПарсерВстроенногоЯзыка\Ext\ObjectModule.bsl", "ПарсерВстроенногоЯзыка");
Парсер = Новый ПарсерВстроенногоЯзыка;
```

Экземпляры плагинов и бакендов создаются аналогично.

Типичный сценарий работы с плагинами, которые регистрируют ошибки:

```bsl
Плагины = Новый Массив;
Плагины.Добавить(Новый ДетекторНеиспользуемыхПеременных);
Плагины.Добавить(Новый ДетекторПропущенныхТочекСЗапятой);
Парсер.Пуск(Исходник, Плагины);
Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(СтрШаблон("%1 [стр: %2; кол: %3]", Ошибка.Текст, Ошибка.НомерСтрокиНачала, Ошибка.НомерКолонкиНачала))
КонецЦикла;
```

Типичный сценарий работы с плагинами, которые генерируют текст:

```bsl
Результаты = ПарсерВстроенногоЯзыка.Пуск(Исходник, Плагин);
Сообщить(СтрСоединить(Результаты));
```

Типичный сценарий работы с плагинами, которые регистрируют замены:

```bsl
Плагины = Новый Массив;
Плагины.Добавить(Новый РасстановкаПропущенныхТочекСЗапятой);
Плагины.Добавить(Новый ЗаменаНеканоничныхКлючевыхСлов);
Парсер.Пуск(Исходник, Плагины);
ИсправленныйИсходник = Парсер.ВыполнитьЗамены();
Сообщить(ИсправленныйИсходник);
```

Типичный сценарий работы с бакендом:

```bsl
АСД = Парсер.Разобрать(Исходник);
Текст = Бакенд.Посетить(Парсер, АСД);
Сообщить(Текст);
```

### Программный интерфейс парсера

#### Основные методы

Данные методы составляют основной программный интерфейс для внешней по отношению к парсеру системы. Позволяют выполнить разбор исходного кода, обход AST с плагинами и выполнить модификацию исходного кода по сгенерированным плагинами заменам.

Чаще всего используется метод `Пуск()`, который заключает в себе типовую последовательность вызовов `Разобрать()`, `Подключить()` и `Посетить()`.
Рабочие примеры использования данных методов смотрите в скриптах в папке /oscript

* **`Пуск(Исходник, Плагины, Параметры, Окружение): Массив`** - выполняет разбор исходника в указанном окружении, обход AST с указанными плагинами и их параметрами и возвращает результаты (обычно текстовые) работы плагинов.
  
  Параметры:
  * `Исходник: Строка` -- исходный код для анализа.
  * `Плагины: Массив или Обработка` -- плагины, с которыми будет выполняться обход AST.
  * `Параметры: Соответствие (необязательный)` -- параметры плагинов.
  * `Окружение: Структура (необязательный)` -- окружение (контекст), в котором будет выполняться разбор исходника.

  Пример с одним плагином:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  
  Плагин = ВнешниеОбработки.Создать(ПлагинПуть, Ложь);
  
  Парсер.Пуск(Исходник, Плагин); // тут выполняется синтаксический анализ и обход АСД с плагинами
  
  Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(Ошибка.Текст); // вывод найденных парсером и плагином ошибок
  КонецЦикла;
  ```

  Пример с набором плагинов:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  
  Плагины = Новый Массив;
  Плагины.Добавить(ВнешниеОбработки.Создать(Плагин1Путь, Ложь));
  Плагины.Добавить(ВнешниеОбработки.Создать(Плагин2Путь, Ложь));
  
  Парсер.Пуск(Исходник, Плагины); // тут выполняется синтаксический анализ и обход АСД с плагинами
  
  Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(Ошибка.Текст); // вывод найденных парсером и плагинами ошибок
  КонецЦикла;
  ```
  
  Пример с параметрами плагинов:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  Плагин1 = ВнешниеОбработки.Создать(Плагин1Путь, Ложь);
  Плагин2 = ВнешниеОбработки.Создать(Плагин2Путь, Ложь);
  
  Плагины = Новый Массив;
  Плагины.Добавить(Плагин1);
  Плагины.Добавить(Плагин2);
  
  Параметры = Новый Соответствие; // у каждого плагина своя структура параметров
  Параметры[Плагин1.ЭтотОбъект] = Новый Структура("Параметр1, Параметр2", 1, 2);
  Параметры[Плагин2.ЭтотОбъект] = Новый Структура("Параметр1, Параметр2", 3, 4)
  
  Парсер.Пуск(Исходник, Плагины, Параметры); // тут выполняется синтаксический анализ и обход АСД с плагинами
  
  Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(Ошибка.Текст); // вывод найденных парсером и плагинами ошибок
  КонецЦикла;
  ```

  Пример с окружением:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  
  Плагин = ВнешниеОбработки.Создать(ПлагинПуть, Ложь));
  
  Окружение = Парсер.Окружение();

  // например, контекст формы:
  Элемент = Парсер.ЭлементОкружения("ЭтаФорма");
  Окружение.Переменные.Вставить("ЭтаФорма", Элемент); // добавили в контекст переменную
  // ...
  Элемент = Парсер.ЭлементОкружения("РеквизитФормыВЗначение");
  Окружение.Методы.Вставить("РеквизитФормыВЗначение", Элемент); // добавили в контекст метод
  // ...
  
  Парсер.Пуск(Исходник, Плагины,, Окружение);
  
  Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(Ошибка.Текст); // вывод найденных парсером и плагином ошибок
  КонецЦикла;
  ```

* **`Разобрать(Исходник, Окружение): Структура`** - выполняет разбор исходника в указанном окружении и возвращает AST.
  
  Параметры:
  * `Исходник: Строка` -- исходный код для анализа.
  * `Окружение: Структура (необязательный)` -- окружение (контекст), в котором будет выполняться разбор исходника.
  
  Пример:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  
  Окружение = Парсер.Окружение();

  // например, контекст формы:
  Элемент = Парсер.ЭлементОкружения("ЭтаФорма");
  Окружение.Переменные.Вставить("ЭтаФорма", Элемент); // добавили в контекст переменную
  // ...
  Элемент = Парсер.ЭлементОкружения("РеквизитФормыВЗначение");
  Окружение.Методы.Вставить("РеквизитФормыВЗначение", Элемент); // добавили в контекст метод
  // ...
  
  АСД = Парсер.Разобрать(Исходник, Окружение);
  
  Сообщить(АСД.Объявления.Количество());
  ```
  
* **`Подключить(Плагины)`** - подключает плагины к парсеру.

  Параметры:
  * `Плагины: Массив или Обработка` -- плагины для подключения к парсеру.
  
  Пример:
  
  ```bsl
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  Плагин = ВнешниеОбработки.Создать(ПлагинПуть, Ложь);

  АСД = Парсер.Разобрать(Исходник); // тут выполняется синтаксический анализ
  Парсер.Подключить(Плагин); // тут плагин подключается к парсеру
  Парсер.Посетить(АСД); // тут выполняется обход АСД с вызовом подписок плагина
  
  Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(Ошибка.Текст); // вывод найденных парсером и плагином ошибок
  КонецЦикла;
  ```

* **`Посетить(Модуль: Структура, Параметры: Соответствие)`** - выполняет обход AST с подключенными плагинами и указанными параметрами плагинов.

  Параметры:
  * `Модуль: Структура` -- АСД для обхода.
  * `Параметры: Соответствие (необязательный)` -- параметры плагинов, с которыми нужно выполнить обход.
  
  Пример:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  Плагин = ВнешниеОбработки.Создать(ПлагинПуть, Ложь);
  
  АСД = Парсер.Разобрать(Исходник); // тут выполняется синтаксический анализ
  Парсер.Подключить(Плагин); // тут плагин подключается к парсеру
  Парсер.Посетить(АСД); // тут выполняется обход АСД с вызовом подписок плагина
  
  Для Каждого Ошибка Из Парсер.ТаблицаОшибок() Цикл
    Сообщить(Ошибка.Текст); // вывод найденных парсером и плагином ошибок
  КонецЦикла;
  ```

* **`Токенизировать(Исходник: Строка): ТаблицаЗначений`** - выполняет лексический анализ и возвращает таблицу токенов.
  
  Параметры:
  * `Исходник: Строка` -- исходный код для анализа.
  
  Пример:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  
  ТаблицаТокенов = Парсер.Токенизировать(Исходник); // тут выполняется лексический анализ
  
  Для Каждого ДанныеТокена Из ТаблицаТокенов Цикл
    Сообщить(ДанныеТокена.Токен); // вывод всех токенов
  КонецЦикла;
  ```

* **`ВыполнитьЗамены(): Строка`** - возвращает исходник после выполнения на нем замен участков текста.

  Пример:
  
  ```bsl
  Исходник = ЧтениеТекста.Прочитать();
  
  Парсер = ВнешниеОбработки.Создать(ПарсерПуть, Ложь);
  Плагин = ВнешниеОбработки.Создать(ПлагинПуть, Ложь);
  
  Парсер.Пуск(Исходник, Плагин); // тут плагин наполняет таблицу замен
  
  ИсправленныйИсходник = Парсер.ВыполнитьЗамены(); // тут по таблице замен последовательно заменяются участки текста в исходнике.
  
  Сообщить(ИсправленныйИсходник)
  ```

#### Дополнительные методы

Данные методы в основном используются в плагинах и вызываются один раз в процедуре Открыть(). Методы предоставляют различную вспомогательную информацию и таблицы, в которых плагины могут регистрировать ошибки и замены.

Примеры использования данных методов смотрите в плагинах в папке /plugins

* **`КлючевыеСлова(): Структура`** - возвращает перечисление допустимых ключевых слов.
  
  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем ТаблицаТокенов, КлючевыеСлова;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    ТаблицаТокенов = Парсер.ТаблицаТокенов();
    КлючевыеСлова = Парсер.КлючевыеСлова();
  КонецПроцедуры
  
  Функция Закрыть() Экспорт
    Для Каждого ДанныеТокена Из ТаблицаТокенов Цикл
      Если ДанныеТокена.Токен = КлючевыеСлова.ИначеЕсли Тогда
        // ...
      КонецЕсли;
    КонецЦикла;
    Возврат Неопределено;
  КонецФункции
  ```

* **`Токены(): Структура`** - возвращает перечисление допустимых токенов.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем ТаблицаТокенов, Токены;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    ТаблицаТокенов = Парсер.ТаблицаТокенов();
    Токены = Парсер.Токены();
  КонецПроцедуры
  
  Функция Закрыть() Экспорт
    Для Каждого ДанныеТокена Из ТаблицаТокенов Цикл
      Если ДанныеТокена.Токен = Токены.ЗнакСложения Тогда
        // ...
      КонецЕсли;
    КонецЦикла;
    Возврат Неопределено;
  КонецФункции
  ```

* **`Типы(): Структура`** - возвращает перечисление допустимых типов узлов AST.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Типы;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Типы = Парсер.Типы();
  КонецПроцедуры

  Процедура ПосетитьОператоры(Операторы) Экспорт
    Для Каждого Оператор Из Операторы Цикл
      Если Оператор.Тип = Типы.ОператорПрисваивания Тогда
        // ...
      КонецЕсли;
    КонецЦикла;
  КонецПроцедуры
  ```
  
* **`Директивы(): Структура`** - возвращает перечисление допустимых директив.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Директивы;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Директивы = Парсер.Директивы();
  КонецПроцедуры
  
  Процедура ПосетитьОбъявлениеПеременнойМодуля(Объявление) Экспорт
      Если Объявление.Директивы.Количество() = 1
        И Объявление[0].Директива = Директивы.НаКлиенте Тогда
        // ...
      КонецЕсли;
  КонецПроцедуры
  ```

* **`Аннотации(): Структура`** - возвращает перечисление допустимых аннотаций.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Аннотации;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Аннотации = Парсер.Аннотации();
  КонецПроцедуры

  Процедура ПосетитьОбъявлениеМетода(Объявление) Экспорт
      Аннотации = Объявление.Сигнатура.Аннотации;
      Если Аннотации.Количество() = 1
        И Аннотации[0].Аннотация = Аннотации.Вместо Тогда
        // ...
      КонецЕсли;
  КонецПроцедуры
  ```

* **`СимволыПрепроцессора(): Структура`** - возвращает перечисление допустимых символов препроцессора.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем СимволыПрепроцессора;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    СимволыПрепроцессора = Парсер.СимволыПрепроцессора();
  КонецПроцедуры
  
  Процедура ПосетитьВыражениеПрепроцессораСимвол(Выражение) Экспорт
    Если Выражение.Символ = СимволыПрепроцессора.ТолстыйКлиентОбычноеПриложение Тогда
      // ...
    КонецЕсли;
  КонецПроцедуры
  ```

* **`Стек(): Массив`** - возвращает стек узлов, который меняется во время обхода AST и позволяет получить текущие родительские узлы в подписках плагинов.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Стек;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Стек = Парсер.Стек();
  КонецПроцедуры
  
  Процедура ПосетитьВыражениеТернарное(Выражение) Экспорт
    Для Каждого Родитель Из Стек Цикл
      Если Родитель.Тип = Типы.ВыражениеТернарное Тогда
        Сообщить("Вложенный тернарный оператор");
        Прервать;
      КонецЕсли;
    КонецЦикла;
  КонецПроцедуры
  ```

* **`Счетчики(): Соответствие`** - возвращает счетчики родительских узлов, которые меняются во время обхода AST и позволяет получить количество текущих родительских узлов по типам в подписках плагинов.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Счетчики;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Счетчики = Парсер.Счетчики();
  КонецПроцедуры
  
  Процедура ПосетитьВыражениеТернарное(Выражение) Экспорт
      Если Счетчики[Типы.ВыражениеТернарное] > 0 Тогда
        Сообщить("Вложенный тернарный оператор");
      КонецЕсли;
  КонецПроцедуры
  ```

* **`Исходник(): Строка`** - возвращает текущий исходник.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Исходник, Токены, ТаблицаТокенов;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Исходник = Парсер.Исходник();
    Токены = Парсер.Токены();
    ТаблицаТокенов = Парсер.ТаблицаТокенов();
  КонецПроцедуры
  
  Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода) Экспорт
    ДанныеСледующегоТокена = ТаблицаТокенов[ОбъявлениеМетода.Конец.Индекс + 1];
    Если ДанныеСледующегоТокена.Токен = Токены.Комментарий Тогда
      Сообщить(Сред(Исходник, ДанныеСледующегоТокена.Позиция, ДанныеСледующегоТокена.Длина));
    КонецЕсли;
  КонецПроцедуры
  ```

* **`ТаблицаТокенов(): ТаблицаЗначений`** - возвращает текущую таблицу токенов, которая содержит подробную информацию о положении каждого токена в исходном коде. Каждый узел AST имеет поля `Начало` и `Конец`, которые хранят строки данной таблицы и представляют первый и последний токен узла. Например, получить номер первой строки узла можно так: `НомерСтроки = Узел.Начало.НомерСтроки`. Получить следующий за узлом токен можно так: `ДанныеСледующегоТокена = ТаблицаТокенов[Узел.Конец.Индекс + 1]`.

  Колонки:
  * `Индекс: Число` -- индекс строки для удобства.
  * `Токен: Строка` -- токен из перечисления Токены.
  * `НомерСтроки: Число` -- номер строки, в которой находится токен.
  * `НомерКолонки: Число` -- номер колонки, в которой начинается токен.
  * `Позиция: Число` -- позиция в тексте, в которой начинается токен.
  * `Длина: Число` -- длина участка текста, который представляет токен.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Исходник, Токены, ТаблицаТокенов;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Исходник = Парсер.Исходник();
    Токены = Парсер.Токены();
    ТаблицаТокенов = Парсер.ТаблицаТокенов();
  КонецПроцедуры
  
  Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода) Экспорт
    ДанныеСледующегоТокена = ТаблицаТокенов[ОбъявлениеМетода.Конец.Индекс + 1];
    Если ДанныеСледующегоТокена.Токен = Токены.Комментарий Тогда
      Сообщить(Сред(Исходник, ДанныеСледующегоТокена.Позиция, ДанныеСледующегоТокена.Длина));
    КонецЕсли;
  КонецПроцедуры
  ```

* **`ТаблицаОшибок(): ТаблицаЗначений`** - возвращает текущую таблицу ошибок, в которой плагины могут регистрировать ошибки.
  
  Колонки:
  * `Источник: Строка` -- имя обработки, которая зарегистрировала ошибку.
  * `Текст: Строка` -- текст ошибки
  * `ПозицияНачала: Число` -- позиция в тексте, с которой начинается ошибочный участок.
  * `ПозицияКонца: Число` -- позиция в тексте, на которой заканчивается ошибочный участок.
  * `ЕстьЗамена: Булево` -- признак, что для ошибки есть замена (исправление) в таблице замен.
  * `Код: Число` -- код ошибки (для плагинов всегда 0).
  * `НомерСтрокиНачала: Число` -- номер строки, на которой начинается ошибочный участок.
  * `НомерКолонкиНачала: Число` -- номер колонки, с которой начинается ошибочный участок.
  * `НомерСтрокиКонца: Число` -- номер строки, на которой заканчивается ошибочный участок.
  * `НомерКолонкиКонца: Число` -- номер колонки, на которой заканчивается ошибочный участок.
  * `МинутНаИсправление: Число` -- примерная оценка затрат на исправление ошибки в минутах.
  * `Серьезность: Строка` -- произвольный текст на усмотрение разработчика.
  * `Приоритет: Число` -- произвольное число на усмотрение разработчика.
  * `Правило: Строка` -- произвольный текст на усмотрение разработчика.
  * `Тип: Строка` -- произвольный текст на усмотрение разработчика.

  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Исходник, Токены, ТаблицаТокенов;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Исходник = Парсер.Исходник();
    Токены = Парсер.Токены();
    ТаблицаТокенов = Парсер.ТаблицаТокенов();
  КонецПроцедуры
  
  Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода) Экспорт

    СледующийТокен = ТаблицаТокенов[ОбъявлениеМетода.Конец.Индекс + 1];

    Если СледующийТокен.Токен = Токены.Комментарий
      И СледующийТокен.НомерСтроки = ОбъявлениеМетода.Конец.НомерСтроки Тогда

      Комментарий = СокрП(Сред(Исходник, СледующийТокен.Позиция, СледующийТокен.Длина));
      ПравильныйКомментарий = СтрШаблон(" %1%2", ОбъявлениеМетода.Сигнатура.Имя, "()"); 

      Если Комментарий <> ПравильныйКомментарий Тогда
        Ошибка("Замыкающий комментарий неактуален", СледующийТокен);
      КонецЕсли;

    КонецЕсли;

  КонецПроцедуры
  
  Процедура Ошибка(Текст, ДанныеТокена)
    Ошибка = ТаблицаОшибок.Добавить();
    Ошибка.Источник = "ИмяЭтогоПлагина";
    Ошибка.Текст = Текст;
    Ошибка.ПозицияНачала = ДанныеТокена.Позиция;
    Ошибка.НомерСтрокиНачала = ДанныеТокена.НомерСтроки;
    Ошибка.НомерКолонкиНачала = ДанныеТокена.НомерКолонки;
    Ошибка.ПозицияКонца = ДанныеТокена.Позиция + ДанныеТокена.Длина;
    Ошибка.НомерСтрокиКонца = ДанныеТокена.НомерСтроки;
    Ошибка.НомерКолонкиКонца = ДанныеТокена.НомерКолонки + ДанныеТокена.Длина;
  КонецПроцедуры
  
  ```

* **`ТаблицаЗамен(): ТаблицаЗначений`** - возвращает текущую таблицу замен, в которой плагины могут регистрировать замены.  
  
  Колонки:
  * `Источник: Число` -- имя обработки, которая зарегистрировала замену.
  * `Текст: Строка` -- фрагмент текста, на который нужно заменить указанный участок.
  * `Позиция: Число` -- позиция участка текста для замены.
  * `Длина: Число` -- длина участка текста для замены.
  
  Пример (фрагмент кода плагина):
  
  ```bsl
  Перем Исходник, Токены, ТаблицаТокенов, ТаблицаЗамен;
  
  Процедура Открыть(Парсер, Параметры) Экспорт
    Исходник = Парсер.Исходник();
    Токены = Парсер.Токены();
    ТаблицаТокенов = Парсер.ТаблицаТокенов();
    ТаблицаЗамен = Парсер.ТаблицаЗамен();
  КонецПроцедуры
  
  Процедура ПосетитьОбъявлениеМетода(ОбъявлениеМетода) Экспорт

    СледующийТокен = ТаблицаТокенов[ОбъявлениеМетода.Конец.Индекс + 1];

    Если СледующийТокен.Токен = Токены.Комментарий
      И СледующийТокен.НомерСтроки = ОбъявлениеМетода.Конец.НомерСтроки Тогда

      Комментарий = СокрП(Сред(Исходник, СледующийТокен.Позиция, СледующийТокен.Длина));
      ПравильныйКомментарий = СтрШаблон(" %1%2", ОбъявлениеМетода.Сигнатура.Имя, "()"); 

      Если Комментарий <> ПравильныйКомментарий Тогда
        Замена(ПравильныйКомментарий, СледующийТокен);
      КонецЕсли;

    КонецЕсли;

  КонецПроцедуры
  
  Процедура Замена(Текст, ДанныеТокена)
    НоваяЗамена = ТаблицаЗамен.Добавить();
    НоваяЗамена.Источник = "ИмяЭтогоПлагина";
    НоваяЗамена.Текст = Текст;
    НоваяЗамена.Позиция = ДанныеТокена.Позиция;
    НоваяЗамена.Длина = ДанныеТокена.Длина;
  КонецПроцедуры
  ```

#### Продвинутые методы

Данные методы позволяют сформировать окружение, в контексте которого будет выполняться разбор исходного кода.

Пример формирования окружения смотрите в скрипте: /oscript/test5.os

Пример формирования глобального окружения смотрите в обработке: /scope/global

* `Окружение(ВнешнееОкружение: Структура): Структура` - конструктор; возвращает новое окружение.
* `ЭлементОкружения(Имя: Строка, Объявление: Структура): Структура` - конструктор; возвращает новый элемент окружения.
* `ОбъявлениеГлобальногоОбъекта(Имя: Строка, Доступность: Структура): Структура` - конструктор; возвращает новый глобальный объект.
* `ОбъявлениеГлобальногоМетода(Имя: Строка, ВозвращаетЗначение: Булево, Параметры: Структура, Доступность: Структура): Структура` - конструктор; возвращает новый глобальный метод.
* `Доступность(Клиент: Булево, ВнешнееСоединение: Булево, МобильноеПриложение: Булево, МобильныйКлиент: Булево, МобильныйСервер: Булево, Сервер: Булево, ТолстыйКлиент: Булево, ТонкийКлиент: Булево, ВебКлиент: Булево, Интеграция: Булево): Структура` - конструктор; возвращает новую структуру доступности.

#### Служебные методы

Эти методы позволяют частично установить состояние парсера без выполнения разбора.
Это необходимо при выполнении обхода кэшированных AST, чтобы плагинам была доступна соответствующая информация при инициализации.

* `УстановитьИсходник(Исходник: Строка)` - устанавливает исходник напрямую без выполнения анализа.
* `УстановитьТаблицуТокенов(ТаблицаТокенов: ТаблицаЗначений)` - устанавливает таблицу токенов напрямую без выполнения анализа.
* `УстановитьТаблицуОшибок(ТаблицаОшибок: ТаблицаЗначений)` - устанавливает таблицу ошибок напрямую без выполнения анализа.

#### Параметры

* `СтрогийРежим: Булево` - определяет поведение парсера при возникновении ошибочных ситуаций.

## Плагины

Плагин реализуется в виде одной обработки.

### Программный интерфейс плагина

Интерфейс плагина состоит из трех постоянных методов и набора методов-подписок.

Например, плагин может подписаться на посещение узла оператора присваивания. В этом случае он должен реализовать экспортный метод `ПосетитьОператорПрисваивания(ОператорПрисваивания)` и вернуть имя этого метода `ПосетитьОператорПрисваивания` в методе `Подписки()`

* `Открыть(Парсер: Обработка, Параметры: Структура)` - вызывается один раз перед обходом AST; позволяет выполнить необходимую инициализацию плагина.
* `Закрыть(): Произвольный` - вызывается один раз после обхода AST, позволяет плагину "подвести итоги" и вернуть результат своей работы. Некоторые плагины всю работу выполняют в данном методе.
* `Подписки(): Массив` - вызывается один раз при подключении плагинов. Возвращает список имен процедур-подписок, которые реализует данный плагин.

## Бакенды

Бакенд реализуется в виде одной обработки.

Бакенды отличаются от плагинов тем, что не используют общий механизм обхода, а выполняют всю работу сами.

Бакенд должен реализовать один метод:

`Посетить(Парсер: Обработка, АСД: Структура): Произвольный` - выполняет произвольную работу и возвращает произвольный результат.
